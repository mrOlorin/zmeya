<!doctype html>
<html lang="ru">
<head>
    <title>Zmeya</title>
    <meta charset="UTF-8">
    <script src="https://aframe.io/releases/1.0.4/aframe.min.js"></script>
    <script id="vertex" type="x-shader/x-vertex">
    uniform float uTime;
    varying vec3 vPosition;
    varying vec3 vLightPosition;
    varying mat3 vCamera;
    // {{position}}
    void main() {
        vPosition = position + getPosition(position);
        vec4 mvPosition = modelViewMatrix * vec4(vPosition, 1.0);
        // Размер точки
        gl_PointSize = 1000. / -mvPosition.z;
        gl_Position = projectionMatrix * mvPosition;

        vLightPosition = (vec4(vPosition - vec3(0., 3., 3.), 1.0) * modelViewMatrix).xyz;
        vec3 viewDirection = vec3(0, 0, -1);
        vec3 forward = normalize(viewDirection);
        vec3 right = normalize(cross(vec3(0., 1., 0.), forward));
        vec3 up = normalize(cross(forward, right));
        vCamera = mat3(right, up, forward);
    }
    </script>
    <script id="fragment" type="x-shader/x-fragment">
    uniform float uTime;
    varying vec3 vPosition;
    varying vec3 vLightPosition;
    varying mat3 vCamera;
    const vec3 gammaCorrection = vec3(1.0 / 2.2);
    struct Material {
        vec3 color;
        float diffuse;
        float specular;
        float ambient;
        float shininess;
        float reflection;
        float transparency;
        float ior;
        float receiveShadows;
    };
    struct HitObject {
        vec3 point;
        float distance;
        Material material;
    };

    // {{distance}}

    // {{material}}

    // https://yandex.ru/search/?text=raymarching
    void rayMarch(inout HitObject obj, in vec3 rayOrigin, in vec3 rayDirection, float plankLength) {
        float stepDistance;
        obj.distance = plankLength;
        for (int i = 0; i < 32; i++) {
            stepDistance = abs(getDistance(rayOrigin + rayDirection * obj.distance));
            obj.distance += stepDistance;
            if (stepDistance < plankLength || obj.distance >= 11.) break;
        }
        obj.point = rayOrigin + rayDirection * obj.distance;
    }
    // Нормаль
    vec3 getNormal(in HitObject hitObject) {
        vec2 offset = vec2(.01, 0);
        return normalize(getDistance(hitObject.point) - vec3(
            getDistance(hitObject.point - offset.xyy),
            getDistance(hitObject.point - offset.yxy),
            getDistance(hitObject.point - offset.yyx)
        ));
    }
    float softShadow(in vec3 point, in vec3 lightDir) {
        point += lightDir * .1;
        float totalDist = .1;
        float result = 1.;
        float d;
        for (int i = 0; i < 32; i ++) {
            d = getDistance(point);
            if (d <= .01) return 0.;
            result = min(result, d / (totalDist * .001));
            totalDist += d;
            if (totalDist > 10.) return result;
            point += lightDir * d;
        }
        return result;
    }
    float ambientOcclusion(in vec3 p, in vec3 n) {
        float k = 1.;
        float occ = 0.;
        float len;
        for (float i = 1.; i < 6.; i += 1.) {
            len = .15 * i;
            occ += (len - getDistance(n * len + p)) * k;
            k *= .5;
        }
        return clamp(1. - occ, 0., 1.);
    }
    // Освещение
    vec3 phongLighting(in HitObject hitObject, in Material mat, in vec3 ray) {
        vec3 normal = getNormal(hitObject);
        vec3 lightDir = normalize(vLightPosition - hitObject.point);
        float shadow = mat.receiveShadows * softShadow(hitObject.point, lightDir) * ambientOcclusion(hitObject.point, normal);
        return (mat.ambient
            + max(0., mat.diffuse * dot(normal, lightDir)) // Diffuse
            * shadow)
            * pow(mat.color, gammaCorrection)
            + pow(max(0., mat.specular * dot(lightDir, reflect(ray, normal))), mat.shininess);// Specular
    }
    // Цвет пикселя
    vec4 getColor(in vec3 origin, in vec3 direction) {
        HitObject hitObject;
        rayMarch(hitObject, origin, direction, .01);
        if (hitObject.distance >= 11.) discard;
        hitObject.material = getMaterial(hitObject.point);
        return vec4(phongLighting(hitObject, hitObject.material, direction), hitObject.material.transparency);
    }
    // Начало
    void main() {
        gl_FragColor = getColor(vec3(0, 0, 0), normalize(vCamera * vec3(gl_PointCoord - vec2(.5), .6)));
    }
    </script>

    <script id="position" type="x-shader/x-vertex">
    varying vec2 wave;
    vec2 cExp(float x) {
        float s = sin(x);
        return vec2(-s, s) + cos(x);
    }
    vec3 getPosition(vec3 p) {
        vec2 e = cExp(p.x - uTime) + cExp(p.x * 0.735 - uTime);
        wave = e;

        // Смещение вдоль осей y и z
        p.yz += wave;

        return p;
    }
    </script>

    <script id="distanceField" type="x-shader/x-fragment">
    varying vec2 wave;
    mat2 rotationMatrix(float a) {
        vec2 cs = vec2(cos(a), sin(a));
        return mat2(cs.x, -cs.y, cs.y, cs.x);
    }
    float getDistance(vec3 p) {
        // Отдаление от внутренней камеры
        p.z -= -5.;

        // Вращение
        p.xy *= rotationMatrix(wave.y);
        p.yz *= rotationMatrix(wave.x);

        // Форма https://iquilezles.org/www/articles/distfunctions/distfunctions.htm
        float sphereRadius = 2.2;
        float sphereDistance = length(p) - sphereRadius;

        float torusRadius = 2.;
        float torusThickness = .5;
        float torusDistance = length(vec2(length(p.xy) - torusRadius, p.z)) - torusThickness;

        float distance = mix(sphereDistance, torusDistance, .5+.5*sin(wave.y+uTime));
        return distance;
    }
    </script>

    <script id="material" type="x-shader/x-fragment">
    Material getMaterial(vec3 p) {
        Material m;

        // Цвет (r, g, b)
        m.color = vec3(wave.x, 0., wave.y);

        m.diffuse = .4;
        m.specular = .3;
        m.ambient = .4;
        m.shininess = 1.;
        m.receiveShadows = 1.;
        m.transparency = 1.;
        return m;
    }
    </script>

    <script>
    AFRAME.registerComponent('points', {
        schema: {
            amount: {type: 'number', default: 10},
        },
        async loadShaders() {
            let vertexShader = document.getElementById('vertex').textContent;
            let fragmentShader = document.getElementById('fragment').textContent
            vertexShader = vertexShader.replace('{{position}}', document.getElementById('position').textContent);
            fragmentShader = fragmentShader.replace('{{distance}}', document.getElementById('distanceField').textContent);
            fragmentShader = fragmentShader.replace('{{material}}', document.getElementById('material').textContent);
            return {vertexShader, fragmentShader};
        },
        async init() {
            this.t = 0;
            const geometry = new THREE.BufferGeometry();
            geometry.setAttribute('position', this.getPositions());
            const {vertexShader, fragmentShader} = await this.loadShaders();
            const material = new THREE.ShaderMaterial({
                uniforms: {uTime: {value: 0}},
                vertexShader,
                fragmentShader,
                blending: THREE.NormalBlending,
                depthTest: true,
                transparent: true
            });
            this.mesh = new THREE.Points(geometry, material);
            this.mesh.frustumCulled = false;
            this.el.setObject3D('mesh', this.mesh);
        },
        tick: function(time, timeDelta) {
            if (!this.mesh) return;
            // Время
            this.t += timeDelta * .001;
            this.mesh.material.uniforms.uTime.value = this.t;
        },
        getPositions: function() {
            const positions = new Float32Array(this.data.amount * 3);
            for (let i = 0, offset = 0, amount = this.data.amount; i < amount; i++, offset = i * 3) {
                // Начальная позиция точки
                positions[offset] = .3 * (i % this.data.amount) - 15.;
                positions[offset + 1] = 2;
                positions[offset + 2] = 0;
            }
            return new THREE.BufferAttribute(positions, 3);
        },
    });
    </script>
</head>
<body>
<a-scene vr-mode-ui="enabled: false" background="color: white">
    <a-entity camera="fov:45" look-controls wasd-controls="acceleration:500;" position="0 5 55"></a-entity>
    <a-entity points="amount: 150"></a-entity>
</a-scene>
</body>
</html>